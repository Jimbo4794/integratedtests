plugins {
    id 'dev.galasa.githash' version '0.1.0-SNAPSHOT' apply false
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'dev.galasa.githash'

    hashes {
        gitHash = System.getenv('GIT_COMMIT')
    }

    group = galasaGroup

    // If a subproject/bundle holds a different version to the root project,
    // <bundle>/gradle.properties should be given the property "projectVersion"
    // e.g. projectVersion=0.1.1
    if (project.hasProperty('projectVersion')){
        version = projectVersion
    } else {
        version = galasaVersion
    }

    // JDK compatibility
    sourceCompatibility = galasaSourceCompatibility
    targetCompatibility = galasaTargetCompatibility

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    def mavenrepo = System.getenv('MAVEN_REPO') ?: "https://nexus.galasa.dev/repository/maven-development"

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }
    
    tasks.withType(PublishToMavenRepository) {
        onlyIf { jar.didWork }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
    
    tasks.withType(Javadoc) {
        failOnError = false
    }

    repositories {
        maven {
            url mavenrepo
        }
        mavenCentral()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                
                pom {
                    name = 'Galasa Testers Programmer Interface (TPI)'
                    url = 'https://galasa.dev'
                    licenses {
                        license {
                            name = 'Eclipse Public License - v 2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0/t'
                        }
                    }
                    developers {
                        developer {
                            name = 'Michael Baylis'
                            email = 'Michael.Baylis@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'William Yates'
                            email = 'wyates@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'David Roberts'
                            email = 'david.roberts@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'James Davies'
                            email = 'james.davies@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Matthew Chivers'
                            email = 'matthew.chivers@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Charlie Meek'
                            email = 'charlie.meek@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Diana Slepikaite'
                            email = 'diana.slepikaite@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git:://github.com/galasa-dev/framework'
                        developerConnection = 'scm:git:git:://github.com/galasa-dev/framework'
                        url = 'https://github.com/galasa-dev/framework'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/galasa-dev/projectmanagement/issues'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username System.getenv('MAVENUSERNAME')
                    password System.getenv('MAVENPASSWORD')
                }
                url = mavenrepo
            }
        }
    }

    signing {
        useGpgCmd() 
        sign publishing.publications.maven 
    }

    dependencies {
        compileOnly 'dev.galasa:dev.galasa:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.framework:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.core.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.zos.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.ipnetwork.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.http.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.artifact.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.linux.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.windows.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.openstack.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.kubernetes.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.docker.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.galasaecosystem.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.java.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.java.ubuntu.manager:0.15.0-SNAPSHOT'
        compileOnly 'dev.galasa:dev.galasa.java.windows.manager:0.15.0-SNAPSHOT'
        compileOnly 'commons-logging:commons-logging:1.2'
        compileOnly 'org.assertj:assertj-core:3.11.0'
        compileOnly 'javax.validation:validation-api:2.0.1.Final'
    }
}
